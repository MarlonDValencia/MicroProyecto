FUNCTION_BLOCK prop	// Definición de bloque

// Definición de las variables de entrada

VAR_INPUT				
	TEMPERATURA : REAL;
	ALTITUD : REAL;
	LUZ : REAL;
	AGUA : REAL;
	PH : REAL;
END_VAR

// Definición de las variables de salida

VAR_OUTPUT				
	CALIDAD_CULTIVO : REAL;
	CONDICION_SUELO : REAL;
END_VAR

// Fuzzificación de la variable de entrada 'TEMPERATURA'
FUZZIFY TEMPERATURA			
	TERM MUY_FRIA := (0, 1) (4.55, 1) (9, 0) ;
	TERM FRIA := trian 8 13 18 ;
	TERM TEMPLADA := trian 16 20 24 ;
	TERM CALIDA := (22, 0) (24, 1) (40, 1) ;
END_FUZZIFY

// Fuzzificación de la variable de entrada 'ALTITUD'
FUZZIFY ALTITUD			
	TERM BAJA := trian 0 0 600 ;
	TERM MEDIA := (460, 0) (700, 1) (1200, 1) (1700, 0) ;
	TERM ALTA := (1500, 0) (1900, 1) (2400, 1) (2750, 0);
	TERM MUY_ALTA := (2460, 0) (2750, 1) (3400, 1) ;
END_FUZZIFY

// Fuzzificación de la variable de entrada 'LUZ'
FUZZIFY LUZ			
	TERM POCA := gauss 0 1.35 ;
	TERM MEDIA := gauss 5.5 0.8 ;
	TERM OPTIMA := gauss 10 1 ;
	TERM SOBREEXPUESTA := trian 12 16 16 ;
END_FUZZIFY

// Fuzzificación de la variable de entrada 'AGUA'
FUZZIFY AGUA			
	TERM POCA := (0, 1) (170, 1) (300, 0) ;
	TERM MODERADA := trian 250 335 420 ;
	TERM CONSIDERABLE := trian 370 510 650 ;
	TERM ABUNDANTE :=  (600, 0) (700, 1) (900, 1);
END_FUZZIFY

// Fuzzificación de la variable de entrada 'PH'
FUZZIFY PH			
	TERM ACIDO := (0, 1) (6, 1) (6.6, 0) ;
	TERM NEUTRO := (6.5, 0) (6.8, 1) (7.2, 1) (7.5, 0) ;
	TERM BASICO :=  (7.3, 0) (8, 1) (14, 1);
END_FUZZIFY

// Defuzzificación de la variable de salida 'CALIDAD_CULTIVO'
DEFUZZIFY CALIDAD_CULTIVO			
	TERM SIN_POTENCIAL := trian 0 0 25 ;
	TERM MALA := trian 15 35 55;
	TERM ACEPTABLE := trian 50 65 80;
	TERM OPTIMA := trian 70 100 100;
	METHOD : COG;		// Usa el metodo de 'Centro de Gravedad'
	DEFAULT := 0;		// Valor por defecto si ninguna regla activa el defuzzificador
END_DEFUZZIFY

// Defuzzificación de la variable de salida 'CONDICION_SUELO'
DEFUZZIFY CONDICION_SUELO			
	TERM MALA := (0, 1) (30, 1) (40, 0) ;
	TERM MEDIA := trian 35 47.5 60 ;
	TERM BUENA := trian 54 72 90 ;
	TERM MUY_BUENA := (85, 0) (93, 1) (100, 1) ;
	METHOD : COG;		// Usa el metodo de 'Centro de Gravedad'
	DEFAULT := 0;		// Valor por defecto si ninguna regla activa el defuzzificador
END_DEFUZZIFY

// Reglas de inferencia

RULEBLOCK No1
	AND : MIN;	// Usa el 'mín' para el 'and' y por defecto el 'máx' para el 'OR'
	ACT : MIN;	// Usa el metodo 'min' para la implicación (trunca)
	ACCU : MAX;	// Usa el 'max' para la agregación

	RULE 1: IF TEMPERATURA IS CALIDA THEN CALIDAD_CULTIVO IS SIN_POTENCIAL;
	RULE 2: IF PH IS ACIDO THEN CALIDAD_CULTIVO IS SIN_POTENCIAL;
	RULE 3: IF PH IS BASICO THEN CALIDAD_CULTIVO IS SIN_POTENCIAL;
	RULE 4: IF TEMPERATURA IS MUY_FRIA THEN CALIDAD_CULTIVO IS SIN_POTENCIAL;
	RULE 5: IF TEMPERATURA IS TEMPLADA AND ALTITUD IS ALTA AND LUZ IS OPTIMA AND AGUA IS CONSIDERABLE AND PH IS NEUTRO THEN CALIDAD_CULTIVO IS OPTIMA;
	RULE 6: IF AGUA IS ABUNDANTE THEN CALIDAD_CULTIVO IS MALA;
	RULE 7: IF TEMPERATURA IS FRIA AND ALTITUD IS MUY_ALTA AND LUZ IS OPTIMA AND AGUA IS MODERADA THEN CALIDAD_CULTIVO IS MALA;
	RULE 8: IF ALTITUD IS BAJA THEN CALIDAD_CULTIVO IS MALA;
	RULE 9: IF LUZ IS POCA THEN CALIDAD_CULTIVO IS SIN_POTENCIAL;
	RULE 10: IF LUZ IS MEDIA AND TEMPERATURA IS TEMPLADA AND AGUA IS MODERADA AND ALTITUD IS ALTA THEN CALIDAD_CULTIVO IS ACEPTABLE;
	RULE 11: IF ALTITUD IS MEDIA AND TEMPERATURA IS TEMPLADA AND AGUA IS CONSIDERABLE AND LUZ IS OPTIMA THEN CALIDAD_CULTIVO IS ACEPTABLE;
	RULE 12: IF TEMPERATURA IS FRIA AND AGUA IS MODERADA AND PH IS NEUTRO THEN CALIDAD_CULTIVO IS OPTIMA;
	RULE 13: IF TEMPERATURA IS FRIA AND LUZ IS SOBREEXPUESTA AND AGUA IS CONSIDERABLE THEN CALIDAD_CULTIVO IS OPTIMA;
	RULE 14: IF ALTITUD IS MUY_ALTA AND TEMPERATURA IS FRIA AND AGUA IS MODERADA AND PH IS NEUTRO THEN CALIDAD_CULTIVO IS ACEPTABLE;
	RULE 15: IF TEMPERATURA IS MUY_FRIA AND ALTITUD IS MUY_ALTA THEN CALIDAD_CULTIVO IS MALA;
	RULE 16: IF AGUA IS POCA THEN CALIDAD_CULTIVO IS SIN_POTENCIAL;
	
	RULE 17: IF TEMPERATURA IS TEMPLADA AND ALTITUD IS ALTA AND LUZ IS OPTIMA AND AGUA IS CONSIDERABLE AND PH IS NEUTRO THEN CONDICION_SUELO IS MUY_BUENA;
	RULE 18: IF PH IS ACIDO THEN CONDICION_SUELO IS MALA;
	RULE 19: IF PH IS BASICO THEN CONDICION_SUELO IS MALA;
	RULE 20: IF AGUA IS ABUNDANTE THEN CONDICION_SUELO IS MALA;
	RULE 21: IF TEMPERATURA IS FRIA AND ALTITUD IS MEDIA AND AGUA IS CONSIDERABLE THEN CONDICION_SUELO IS MEDIA;
	RULE 22: IF TEMPERATURA IS FRIA AND ALTITUD IS ALTA AND AGUA IS MODERADA AND PH IS NEUTRO THEN CONDICION_SUELO IS BUENA;
	RULE 23: IF PH IS NEUTRO AND TEMPERATURA IS MUY_FRIA THEN CONDICION_SUELO IS MALA;
	RULE 24: IF PH IS NEUTRO AND LUZ IS POCA THEN CONDICION_SUELO IS MALA;
	RULE 25: IF PH IS NEUTRO AND AGUA IS ABUNDANTE THEN CONDICION_SUELO IS MALA;

	RULE 26: IF TEMPERATURA IS TEMPLADA AND ALTITUD IS ALTA AND LUZ IS SOBREEXPUESTA AND AGUA IS CONSIDERABLE AND PH IS NEUTRO THEN CALIDAD_CULTIVO IS ACEPTABLE;

END_RULEBLOCK

END_FUNCTION_BLOCK
